"""bazel-orfs dependencies"""

module(
    name = "bazel-orfs",
    version = "0.0.1",
    compatibility_level = 1,
)

bazel_dep(name = "bazel_skylib", version = "1.9.0")

# NOTE! downgraded googletest to 1.15.2 from 1.17.0 to avoid target 'if_constexpr' error
bazel_dep(name = "googletest", version = "1.15.2")
bazel_dep(name = "rules_jvm_external", version = "6.7")
bazel_dep(name = "rules_java", version = "8.14.0")
bazel_dep(name = "rules_scala", version = "7.1.5")
bazel_dep(name = "aspect_rules_js", version = "2.1.3")
bazel_dep(name = "rules_verilator", version = "0.1.0")
bazel_dep(name = "verilator", version = "5.036.bcr.3")
bazel_dep(name = "rules_shell", version = "0.3.0")
bazel_dep(name = "rules_cc", version = "0.2.4")

####### Node.js version #########
# By default you get the node version from DEFAULT_NODE_VERSION in @rules_nodejs//nodejs:repositories.bzl
# Optionally you can pin a different node version:
bazel_dep(name = "rules_nodejs", version = "6.3.0")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(node_version = "16.14.2")
#################################

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    pnpm_lock = "//:pnpm-lock.yaml",
    # verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(npm, "npm")

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")

# Allows developers to use the matching pnpm version, for example:
# bazel run -- @pnpm --dir /home/runner/work/rules_js/rules_js install
use_repo(pnpm, "pnpm")

http_archive = use_repo_rule(
    "@bazel_tools//tools/build_defs/repo:http.bzl",
    "http_archive",
)

http_archive(
    name = "oss_cad_suite",
    build_file = "oss_cad_suite.BUILD.bazel",
    sha256 = "05ff3c1435b7ce69f537ef942c6e0e96d09b10fac350e99925a3dd1610246f30",
    strip_prefix = "oss-cad-suite",
    urls = [
        "https://github.com/YosysHQ/oss-cad-suite-build/releases/download/2025-11-18/oss-cad-suite-linux-x64-20251118.tgz",
    ],
)

load_json_file = use_repo_rule("//:load_json_file.bzl", "load_json_file")

load_json_file(
    name = "orfs_variable_metadata",
    src = "@docker_orfs//:OpenROAD-flow-scripts/flow/scripts/variables.yaml",
    # Dummy output file, we're not actually using it
    out = "variables.json",
    script = "//:yaml_to_json.py",
)

bazel_dep(
    name = "rules_python",
    version = "1.6.3",
)

python = use_extension(
    "@rules_python//python/extensions:python.bzl",
    "python",
)
python.toolchain(
    ignore_root_user_error = True,
    python_version = "3.13",
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "bazel-orfs-pip",
    python_version = "3.13",
    requirements_lock = "//:requirements_lock_3_13.txt",
)
use_repo(pip, "bazel-orfs-pip")

orfs = use_extension("//:extension.bzl", "orfs_repositories")
orfs.default(
    # a local only or remote docker image. Local docker images do not
    # have a sha256.
    image = "docker.io/openroad/orfs:26Q1-170-g5dcd585a9",
    # Smoketest, this is the default value
    makefile = "@docker_orfs//:makefile",
    # Comment out line below for local only docker images
    sha256 = "dcbf5fbae74c10ef346c6d65719c99d93f7c2b2750cba96e8829447e93468c83",
)
use_repo(orfs, "com_github_nixos_patchelf_download")
use_repo(orfs, "docker_orfs")
use_repo(orfs, "config")

# Not in use in CI
#
# pin = use_extension("//extensions:pin.bzl", "pin")
# pin.artifacts(
#     artifacts_lock = "//:artifacts_lock.txt",
#     repo_name = "pinned",
# )
# use_repo(pin, "pinned")

SCALA_VERSION = "2.13.17"

SCALA_VERSION_SHORT = SCALA_VERSION.rpartition(".")[0]

CIRCE_VERSION = "0.14.4"

MAVEN_DEPS = {
    "org.apache.commons:commons-text": "1.12.0",
    # BCR rules_scala automatically provides scala-compiler, scala-library, scala-reflect
}

MAVEN_SHORT_DEPS = {
    "com.github.scopt:scopt": "4.1.0",
    "com.lihaoyi:os-lib": "0.10.0",
    "com.lihaoyi:ujson": "4.1.0",
    "com.lihaoyi:upickle": "4.1.0",
    "io.circe:circe-core": CIRCE_VERSION,
    "io.circe:circe-generic": CIRCE_VERSION,
    "io.circe:circe-generic-extras": CIRCE_VERSION,
    "io.circe:circe-parser": CIRCE_VERSION,
    "org.chipsalliance:chisel": "7.2.0",
    "org.chipsalliance:firtool-resolver": "2.0.1",
    "org.json4s:json4s-native": "4.0.7",
    "org.scalatest:scalatest": "3.2.19",
}

MAVEN_LONG_DEPS = {
    "org.chipsalliance:chisel-plugin": "7.2.0",
    "org.scalameta:semanticdb-scalac": "4.14.1",
}

# Configure BCR rules_scala
scala_config = use_extension("@rules_scala//scala/extensions:config.bzl", "scala_config")
scala_config.settings(scala_version = "2.13.17")
use_repo(scala_config, "rules_scala_config")

scala_deps = use_extension("@rules_scala//scala/extensions:deps.bzl", "scala_deps")
scala_deps.scala()  # Provides scala-compiler, scala-library, scala-reflect
scala_deps.scalatest()  # Provides scalatest framework
use_repo(scala_deps, "rules_scala_toolchains")

# Register custom Scala toolchain with SemanticDB enabled
#
# NOTE! Do not enable because it causes problems for e.g. OpenROAD
# that does not need or want semanticdb toolchain
#
# register_toolchains("//:semanticdb_toolchain")

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    artifacts = [
                    "{}_{}:{}".format(dep, SCALA_VERSION_SHORT, version)
                    for (dep, version) in MAVEN_SHORT_DEPS.items()
                ] +
                [
                    "{}_{}:{}".format(dep, SCALA_VERSION, version)
                    for (dep, version) in MAVEN_LONG_DEPS.items()
                ] +
                ["{}:{}".format(dep, version) for (dep, version) in MAVEN_DEPS.items()],
    fetch_sources = True,
    # bazelisk run @maven//:pin
    lock_file = "//:maven_install.json",
    repositories = [
        "https://repo1.maven.org/maven2",  # fastest mirror, last I tried
        "https://s01.oss.sonatype.org/content/repositories/releases",
        "https://s01.oss.sonatype.org/content/repositories/snapshots",
    ],
)
use_repo(maven, "maven")

# ## Lock the compiler version and avoid any local compiler
# bazel_dep(name = "toolchains_llvm", version = "1.5.0")

# # Configure and register the toolchain.
# llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
# llvm.toolchain(
#     llvm_version = "20.1.8",
# )
# use_repo(llvm, "llvm_toolchain")

register_toolchains(
    "@rules_verilator//verilator:verilator_toolchain",
    "@rules_scala_toolchains//...:all",
    # "@llvm_toolchain//:all",
)

http_archive(
    name = "circt",
    build_file_content = """
package(default_visibility = ["//visibility:public"])

filegroup(
    name = "bin/firtool",
    srcs = ["org.chipsalliance/llvm-firtool/linux-x64/bin/firtool"],
)

exports_files(["org.chipsalliance/llvm-firtool/linux-x64/bin/firtool"])
""",
    sha256 = "87eed8ee81914c9bce19228a6b397b10a7e8b1a8dbafa100ed84c15bc63a8188",
    urls = ["https://repo1.maven.org/maven2/org/chipsalliance/llvm-firtool/1.137.0/llvm-firtool-1.137.0.jar"],
)
