load("@bazel-orfs//:openroad.bzl", "orfs_pdk")

exports_files(
    glob(
        [
            "OpenROAD-flow-scripts/**/*.tcl",
            "OpenROAD-flow-scripts/flow/platforms/asap7/**/*.v",
        ],
    ),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "opengl",
    srcs = glob(["usr/lib/x86_64-linux-gnu/dri/*.so"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "qt_plugins",
    srcs = glob(["usr/lib/x86_64-linux-gnu/qt5/plugins/**"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "ld.so",
    srcs = ["lib64/ld-linux-x86-64.so.2"],
)

filegroup(
    name = "openroad",
    data = [
        ":ld.so",
        ":qt_plugins",
    ],
    srcs = ["OpenROAD-flow-scripts/tools/install/OpenROAD/bin/openroad"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "sta",
    data = [
        ":ld.so",
    ],
    srcs = ["OpenROAD-flow-scripts/tools/install/OpenROAD/bin/sta"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "yosys",
    data = [
        ":ld.so",
        "OpenROAD-flow-scripts/tools/install/yosys/share/yosys/plugins/slang.so",
    ],
    srcs = ["OpenROAD-flow-scripts/tools/install/yosys/bin/yosys"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "make",
    data = [
        ":ld.so",
    ],
    srcs = ["usr/bin/make"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "yosys-abc",
    data = [
        ":ld.so",
    ],
    srcs = ["OpenROAD-flow-scripts/tools/install/yosys/bin/yosys-abc"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "klayout",
    data = [
        ":ld.so",
    ],
    srcs = ["usr/bin/klayout"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "tcl8.6",
    srcs = glob(["usr/share/tcltk/tcl8.6/**"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "ruby3.0.0",
    srcs = glob(["usr/lib/ruby/3.0.0/**"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "ruby_dynamic3.0.0",
    srcs = glob(["usr/lib/x86_64-linux-gnu/ruby/3.0.0/**"]),
    visibility = ["//visibility:public"],
)

MAKEFILE_SHARED = [
    "OpenROAD-flow-scripts/flow/util/utils.mk",
    "OpenROAD-flow-scripts/flow/util/*.py",
    "OpenROAD-flow-scripts/flow/util/*.sh",
    "OpenROAD-flow-scripts/flow/scripts/*.py",
    "OpenROAD-flow-scripts/flow/scripts/*.sh",
    "OpenROAD-flow-scripts/flow/scripts/*.yaml",
    "OpenROAD-flow-scripts/flow/scripts/*.mk",
]

# Narrowly define yosys dependencies to avoid hours of resynthesis
# upon upgrading ORFS since yosys hardly ever changes
filegroup(
    name = "makefile_yosys",
    srcs = ["OpenROAD-flow-scripts/flow/Makefile"],
    data = glob(
        MAKEFILE_SHARED +
        [
            "OpenROAD-flow-scripts/flow/scripts/*.script",
            "OpenROAD-flow-scripts/flow/scripts/util.tcl",
            "OpenROAD-flow-scripts/flow/scripts/synth*.tcl",
            "OpenROAD-flow-scripts/flow/scripts/**/*.v",
        ],
    ),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "makefile",
    srcs = ["OpenROAD-flow-scripts/flow/Makefile"],
    data = glob(
        MAKEFILE_SHARED +
        [
            "OpenROAD-flow-scripts/flow/scripts/*.tcl",
        ],
    ),
    visibility = ["//visibility:public"],
)

[
    orfs_pdk(
        name = pdk,
        config = ":OpenROAD-flow-scripts/flow/platforms/{pdk}/config.mk".format(pdk = pdk),
        srcs = glob(
            [
                "OpenROAD-flow-scripts/flow/platforms/{pdk}/**/*.{ext}".format(
                    pdk = pdk,
                    ext = ext,
                )
                for ext in [
                               "gds",
                               "lef",
                               "lyt",
                               "mk",
                               "rules",
                               "tcl",
                               "v",
                           ] +
                           {
                               "sky130hd": ["tlef"],
                               "asap7": ["sdc"],
                           }.get(pdk, [])
            ] +
            ["OpenROAD-flow-scripts/flow/platforms/common/**/*.v"],
        ),
        libs = glob(
            [
                "OpenROAD-flow-scripts/flow/platforms/{pdk}/**/*.{ext}".format(
                    pdk = pdk,
                    ext = ext,
                )
                for ext in [
                               "lib",
                           ] +
                           {
                               "asap7": ["lib.gz"],
                           }.get(pdk, [])
            ],
        ),
        visibility = ["//visibility:public"],
    )
    for pdk in [
        "asap7",
        "sky130hd",
        "ihp-sg13g2",
    ]
]
